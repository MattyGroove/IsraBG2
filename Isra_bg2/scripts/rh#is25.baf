// ---------------------------------------------
// Initial Go-To
// ---------------------------------------------

IF
    NumTimesTalkedTo(0)
THEN
  RESPONSE #100
    Dialogue(Player1)
END

// ---------------------------------------------
// Dialogue Track
// ---------------------------------------------

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
	OR(2)
		Global("rh#IsraTalksToB","GLOBAL",3)
    	Global("rh#IsraTalksToB","GLOBAL",7)
THEN
  RESPONSE #100
  	PlaySong(0)
    PlaySound("rh#isong")
	Dialogue(Player1)
END

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
 	Global("rh#IsraRomanceActive","GLOBAL",2)
    OR(4)
    	Global("rh#IsraTalksToB","GLOBAL",5)
    	Global("rh#IsraTalksToB","GLOBAL",9)
    	Global("rh#IsraTalksToB","GLOBAL",11)
    	Global("rh#IsraFinalTalk","GLOBAL",2)
THEN
  RESPONSE #100
  	PlaySong(0)
    PlaySound("rh#isong")
	Dialogue(Player1)
END

// incrementing

// 2.

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    RealGlobalTimerExpired("rh#IsraTalksToBTimer","GLOBAL")
    Global("rh#IsraTalksToB","GLOBAL",2)
THEN
  RESPONSE #100
	IncrementGlobal("rh#IsraTalksToB","GLOBAL",1)
END

// 3.

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    Global("rh#IsraRomanceActive","GLOBAL",2)
    RealGlobalTimerExpired("rh#IsraTalksToBTimer","GLOBAL")
    Global("rh#IsraTalksToB","GLOBAL",4)
THEN
  RESPONSE #100
	IncrementGlobal("rh#IsraTalksToB","GLOBAL",1)
END

// 4.

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    RealGlobalTimerExpired("rh#IsraTalksToBTimer","GLOBAL")
    GlobalGT("METBAL","GLOBAL",0)
 	Global("rh#IsraTalksToB","GLOBAL",6)
THEN
  RESPONSE #100
	IncrementGlobal("rh#IsraTalksToB","GLOBAL",1)
END

// 5, 6

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    Global("rh#IsraRomanceActive","GLOBAL",2)
    RealGlobalTimerExpired("rh#IsraTalksToBTimer","GLOBAL")
    GlobalGT("METBAL","GLOBAL",0)
    GlobalLT("rh#IsraSettled","GLOBAL",2)
    OR(2)
    	Global("rh#IsraTalksToB","GLOBAL",8)
    	Global("rh#IsraTalksToB","GLOBAL",10)
THEN
  RESPONSE #100
	IncrementGlobal("rh#IsraTalksToB","GLOBAL",1)
END

// final talk

IF
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    Global("rh#IsraRomanceActive","GLOBAL",2)
	Global("ChallengesDone","GLOBAL",1)
	Global("rh#IsraFinalTalk","GLOBAL",0)
THEN
RESPONSE #100
	SetGlobal("rh#IsraFinalTalk","GLOBAL",1)
	RealSetGlobalTimer("rh#IsraFinalTalkTimer","GLOBAL",30)
END

IF
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    ReputationGT(Player1,9)
    Global("rh#IsraRomanceActive","GLOBAL",2)
	Global("ChallengesDone","GLOBAL",1)
	Global("rh#IsraFinalTalk","GLOBAL",1)
	RealGlobalTimerExpired("rh#IsraFinalTalkTimer","GLOBAL")
THEN
RESPONSE #100
	SetGlobal("rh#IsraFinalTalk","GLOBAL",2)
END

IF
    !Global("rh#IsraRomanceActive","GLOBAL",2)
	Global("rh#IsraTalksToB","GLOBAL",4)
THEN
RESPONSE #100
	SetGlobal("rh#IsraTalksToB","GLOBAL",6)
END

// ---------------------------------------------
// Plot Related Dialogue
// ---------------------------------------------

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    OR(4)
    	Global("rh#IsraWraith","GLOBAL",3)
		Global("rh#IsraOasis","GLOBAL",1)
		Global("rh#IsraChallengesTalk","GLOBAL",2)
		Global("rh#IsraDraconis","GLOBAL",1)
THEN
  RESPONSE #100
	Dialogue(Player1)
END

// Wraith

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("rh#IsraRomanceActive","GLOBAL",2)
    Global("rh#IsraWraith","GLOBAL",2)
THEN
  RESPONSE #100
	IncrementGlobal("rh#IsraWraith","GLOBAL",1)
END

// Oasis

IF
	InParty("rh#Isra2")
	See(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
	Dead("amtgen01")
	AreaCheck("AR6300")
	GlobalGT("TethyrBattleStart","GLOBAL",0)
	Global("rh#IsraOasis","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("rh#IsraOasis","GLOBAL",1)
END

// Third Challenge

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    !Global("rh#IsraRomanceActive","GLOBAL",2)
	GlobalGT("BeginChallenge3","GLOBAL",2)
	Global("rh#IsraChallengesTalk","GLOBAL",0)
THEN
  RESPONSE #100
	RealSetGlobalTimer("rh#IsraTalksChallengeTimer","GLOBAL",60)
	SetGlobal("rh#IsraChallengesTalk","GLOBAL",1)
END
	
IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    !Global("rh#IsraRomanceActive","GLOBAL",2)
	RealGlobalTimerExpired("rh#IsraTalksChallengeTimer","GLOBAL")
	Global("rh#IsraChallengesTalk","GLOBAL",1)
THEN
  RESPONSE #100
	SetGlobal("rh#IsraChallengesTalk","GLOBAL",2)
END

// Draconis

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    AreaCheck("AR6000")
    Dead("bazdra02")
    Global("rh#IsraDraconis","GLOBAL",0)
THEN
  RESPONSE #100
	IncrementGlobal("rh#IsraDraconis","GLOBAL",1)
END

// ---------------------------------------------
// Reputational & Alignment Issues
// ---------------------------------------------

IF	
	InParty("rh#Isra2")
	See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
	OR(4)
		Global("rh#IsraLowReputation","GLOBAL",1)
		Global("rh#IsraReputationBreak","GLOBAL",1)
		Global("rh#IsraPCEvil","GLOBAL",1)
		Global("rh#IsraFallenPally","GLOBAL",1)
 THEN
RESPONSE #100
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
    !ReputationGT(Player1,9)
	Global("rh#IsraLowReputation","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("rh#IsraLowReputation","GLOBAL",1)
END

IF
	InParty(Myself)
    !ReputationGT(Player1,5)
	Global("rh#IsraReputationBreak","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("rh#IsraReputationBreak","GLOBAL",1)
END


IF
	InParty("rh#Isra2")
	AreaCheck("AR4000")
	Alignment(Player1,MASK_EVIL)
	Global("rh#IsraPCEvil","GLOBAL",0)
 THEN
RESPONSE #100
	SetGlobal("rh#IsraPCEvil","GLOBAL",1)
END

// if you never got her paladinhood back

IF
	InParty(Myself)
	AreaCheck("AR4000")
	Global("rh#IsraVampire","GLOBAL",6)
	GlobalLT("rh#IsraPaladin","GLOBAL",6)
	Global("rh#IsraFallenPally","GLOBAL",0)
 THEN
RESPONSE #100
	SetGlobal("rh#IsraFallenPally","GLOBAL",1)
END

// ---------------------------------------------
// Flirts
// ---------------------------------------------

IF 
	Global("rh#IsraStartFlirts","GLOBAL",0)
	Global("rh#IsraRomanceActive","GLOBAL",2)
 THEN
RESPONSE #100
	SetGlobal("rh#IsraStartFlirts","GLOBAL",1)
	RealSetGlobalTimer("rh#IsraFlirtTimer","GLOBAL",1800)
END

IF
	InParty("rh#Isra2")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	RealGlobalTimerExpired("rh#IsraFlirtTimer","GLOBAL")
	!Global("rh#IsraFlirtsDisabled","GLOBAL",1)
	Global("rh#IsraTimeForFlirt","GLOBAL",0)
	Global("rh#IsraRomanceActive","GLOBAL",2)
 THEN
RESPONSE #100	
	SetGlobal("rh#IsraTimeForFlirt","GLOBAL",1)
END

IF
	InParty("rh#Isra2")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	RealGlobalTimerExpired("rh#IsraFlirtTimer","GLOBAL")
	!Global("rh#IsraFlirtsDisabled","GLOBAL",1)
	Global("rh#IsraTimeForFlirt","GLOBAL",1)
	Global("rh#IsraRomanceActive","GLOBAL",2)
 THEN
RESPONSE #100
	Dialogue(Player1)
END

// IF HOSTILE

IF
	Allegiance(Myself,ENEMY)
	!InParty(Myself)
	Global("rh#NPCFight","LOCALS",0)
THEN
  RESPONSE #100
	ChangeAIScript("rh#isrx",CLASS)
	ChangeAIScript("useitem",GENERAL)
	ChangeAIScript("",DEFAULT)
	SetGlobal("rh#NPCFight","LOCALS",1)
END